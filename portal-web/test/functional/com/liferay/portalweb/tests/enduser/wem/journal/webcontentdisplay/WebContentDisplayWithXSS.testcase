@component-name = "portal-wcm"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Web Content Display";

	setUp {
		task ("Set up instance and sign in") {
			TestCase.setUpPortalInstance();

			User.firstLoginPG();
		}

		task ("Add a site") {
			JSONGroup.addGroup(groupName = "Test Site Name");
		}
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			JSONGroup.deleteGroupByName(groupName = "Test Site Name");
		}
	}

	@description = "This is a test for LPS-117985. Select the web content based on template name with XSS on WCD."
	@priority = "4"
	@uitest
	test SelectWebContentBasedOnTemplateNameWithXSS {
		task ("Add a web content template with a XSS name") {
			var actualScript = '''<script>alert(123);</script>''';

			var escapedScript = '''&lt;script&gt;alert(123);&lt;/script&gt;''';

			WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

			NavItem.gotoTemplates();

			WebContentTemplates.addCP(
				structureName = "Basic Web Content",
				templateName = "${actualScript}");
		}

		task ("View the alert not shown") {
			AssertAlertNotPresent();
		}

		task ("View the XSS is escaped") {
			LexiconCard.viewCardNotPresent(card = "${actualScript}");

			WebContentTemplates.viewCardsEntryCP(templateName = "${escapedScript}");
		}

		task ("Add a web content based on new template") {
			NavItem.gotoWebContent();

			WebContentNavigator.gotoAddCP();

			WebContent.addCP(
				structureName = "Basic Web Content",
				webContentTitle = "Web Content Title");

			WebContent.selectTemplateCP(
				structureName = "Basic Web Content",
				templateName = "${escapedScript}");

			PortletEntry.publish();
		}

		task ("Add a widget page") {
			JSONLayout.addPublicLayout(
				groupName = "Test Site Name",
				layoutName = "Test Page Name");
		}

		task ("Add a Web Content Display to page") {
			JSONLayout.addWidgetToPublicLayout(
				groupName = "Test Site Name",
				layoutName = "Test Page Name",
				widgetName = "Web Content Display");
		}

		task ("Select the web content in Web Content Display") {
			Navigator.gotoSitePage(
				pageName = "Test Page Name",
				siteName = "Test Site Name");

			WebContentDisplayPortlet.selectWebContent(webContentTitle = "Web Content Title");
		}

		task ("View the alert not shown") {
			AssertAlertNotPresent();
		}
	}

}