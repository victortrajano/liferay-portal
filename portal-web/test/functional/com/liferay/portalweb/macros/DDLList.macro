definition {

	var ddlDataDefinitionDescription = "Data Definition Description";
	var ddlDataDefinitionName = "Data Definition Name";
	var ddlListName = "List Name";

	macro addCP {
		LexiconEntry.gotoAdd();

		Type.clickAtType(
			locator1 = "TextInput#NAME",
			value1 = "${ddlListName}");

		if (isSet(ddlListDescription)) {
			Type.clickAtType(
				locator1 = "TextArea#DESCRIPTION",
				value1 = "${ddlListDescription}");
		}

		if (isSet(workflowDefinition)) {
			Select(
				locator1 = "DDLEditList#WORKFLOW_SELECT",
				value1 = "${workflowDefinition}");
		}

		AssertClick(
			locator1 = "DDLEditList#DATA_DEFINITION_SELECT",
			value1 = "Select");
	}

	macro addViaDDLDisplayPG {
		Click(
			locator1 = "DDL#PORTLET_ACTIONS_ADD_LIST",
			value1 = "Add List");

		AssertTextEquals(
			locator1 = "Portlet#MODAL_TITLE",
			value1 = "Add List");

		SelectFrame(locator1 = "IFrame#MODAL_BODY");

		PortletEntry.inputName(name = "${ddlListName}");

		if (isSet(ddlListDescription)) {
			PortletEntry.inputDescription(description = "${ddlListDescription}");
		}

		if (isSet(workflowDefinition)) {
			Select(
				locator1 = "DDLEditList#WORKFLOW_SELECT",
				value1 = "${workflowDefinition}");
		}

		AssertClick(
			locator1 = "DDLEditList#DATA_DEFINITION_SELECT",
			value1 = "Select");

		SelectFrameTop();
	}

	macro assertCannotDeleteRecordViaDDLDisplayPG {
		Click(locator1 = "DDL#RECORD_TABLE_ELLIPSIS_1");

		MenuItem.viewNotPresent(menuItem = "Delete");

		MenuItem.viewPresent(menuItem = "View");
	}

	macro deleteCP {
		LexiconEntry.viewEntryName(rowEntry = "${ddlListName}");

		LexiconEntry.gotoEllipsisMenuItemNoError(menuItem = "Delete");

		AssertConfirm(value1 = "Are you sure you want to delete this? It will be deleted immediately.");

		Alert.viewSuccessMessage();

		LexiconEntry.viewNoEntry(rowEntry = "${ddlListName}");
	}

	macro editCP {
		LexiconEntry.viewEntryName(rowEntry = "${ddlListName}");

		LexiconEntry.gotoEllipsisMenuItem(menuItem = "Edit");

		Type.clickAtType(
			locator1 = "TextInput#NAME",
			value1 = "${ddlListNameEdit}");

		if (isSet(ddlListDescriptionEdit)) {
			Type.clickAtType(
				locator1 = "TextArea#DESCRIPTION",
				value1 = "${ddlListDescriptionEdit}");
		}

		Click(locator1 = "Button#SAVE");

		Alert.viewSuccessMessage();
	}

	macro editPermissions {
		Search.searchCP(searchTerm = "${ddlListName}");

		LexiconEntry.gotoEllipsisMenuItem(menuItem = "Permissions");
	}

	macro editUserViewPermissionCP {
		ProductMenu.gotoPortlet(
			category = "Content & Data",
			portlet = "Dynamic Data Lists");

		DDLList.editPermissions(ddlListName = "${ddlListName}");

		if (IsElementPresent.isElementPresentAfterWait(locator1 = "IFrame#MODAL_BODY")) {
			SelectFrame(locator1 = "IFrame#MODAL_BODY");
		}

		PermissionsInline.addOrRemoveViewPermissionFromAllRoles(
			addOrRemoveViewPermission = "${addOrRemoveViewPermission}",
			roleList = "${roleList}");
	}

	macro editViaDDLDisplayPG {
		Click(
			locator1 = "DDL#PORTLET_ACTIONS_EDIT_LIST",
			value1 = "Edit List");

		SelectFrame(locator1 = "IFrame#MODAL_BODY");

		PortletEntry.inputName(name = "${ddlListNameEdit}");

		if (isSet(ddlListDescription)) {
			PortletEntry.inputDescription(description = "${ddlListDescriptionEdit}");
		}

		AssertClick(
			locator1 = "Button#SAVE",
			value1 = "Save");
	}

	macro openDynamicDataListsAdmin {
		Navigator.openWithAppendToBaseURL(urlAppend = "group/${siteURLKey}/~/control_panel/manage?p_p_id=com_liferay_dynamic_data_lists_web_portlet_DDLPortlet");
	}

	macro saveViaDDLDisplayPG {
		DDLDataDefinition.save();

		Pause(locator1 = "5000");
	}

	macro selectViaDDLDisplayPG {
		Click(
			locator1 = "DDL#PORTLET_ACTIONS_SELECT_LIST",
			value1 = "Select List");

		SelectFrame(locator1 = "DDLConfiguration#CONFIGURATION_IFRAME");

		var key_ddlListName = "${ddlListName}";

		AssertClick(
			locator1 = "DDLConfiguration#LIST_TABLE_NAME",
			value1 = "${ddlListName}");

		AssertTextEquals(
			locator1 = "Message#INFO",
			value1 = "Displaying List: ${ddlListName} (Modified)");

		AssertClick(
			locator1 = "Button#SAVE",
			value1 = "Save");

		Alert.viewSuccessMessage();

		SelectFrameTop();
	}

	macro tearDownCP {
		Navigator.openURL();

		ProductMenu.gotoPortlet(
			category = "Content & Data",
			portlet = "Dynamic Data Lists");

		while (IsElementPresent(locator1 = "Icon#BODY_VERTICAL_ELLIPSIS")) {
			LexiconEntry.gotoEllipsisMenuItemNoError(menuItem = "Delete");

			AssertConfirm(value1 = "Are you sure you want to delete this? It will be deleted immediately.");

			Alert.viewSuccessMessage();
		}
	}

	macro viewCP {
		LexiconEntry.viewEntryName(rowEntry = "${ddlListName}");

		if (isSet(ddlListDescription)) {
			var key_ddlListDescription = "${ddlListDescription}";

			AssertTextEquals(
				locator1 = "DDL#LIST_TABLE_DESCRIPTION",
				value1 = "${ddlListDescription}");
		}

		LexiconEntry.gotoEntry(rowEntry = "${ddlListName}");

		SiteAdmin.viewPortletTitle(portletTitle = "${ddlListName}");
	}

	macro viewDeleteRecordOptionViaDDLDisplayPG {
		Click(locator1 = "DDL#RECORD_TABLE_ELLIPSIS_1");

		MenuItem.viewPresent(menuItem = "Delete");
	}

	macro viewViaDDLDisplayPG {
		AssertTextEquals(
			locator1 = "Portlet#H2_HEADER",
			value1 = "${ddlListName}");
	}

}