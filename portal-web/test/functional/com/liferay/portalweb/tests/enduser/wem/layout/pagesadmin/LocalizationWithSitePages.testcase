@component-name = "portal-wcm"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Site Pages Administration";

	setUp {
		task ("Set up instance and sign in") {
			TestCase.setUpPortalInstance();

			User.firstLoginPG();
		}

		task ("Add a site") {
			JSONGroup.addGroup(groupName = "Test Site Name");
		}
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			JSONGroup.deleteGroupByName(groupName = "Test Site Name");
		}
	}

	@description = "Can add a translation to parent and child page."
	@priority = "3"
	test AddTranslationToPageName {
		property testray.component.names = "Localization,Site Administration,Site Localization";

		task ("Get portal URL") {
			var portalURL = PropsUtil.get("portal.url");
		}

		task ("Add widget page") {
			JSONLayout.addPublicLayout(
				groupName = "Test Site Name",
				layoutName = "Home");
		}

		task ("Add a translation of page to es-ES") {
			PagesAdmin.openPagesAdmin(siteURLKey = "test-site-name");

			PagesAdmin.gotoPageEllipsisMenuItem(
				menuItem = "Configure",
				pageName = "Home");

			PagesAdmin.addLocalizedName(
				localeList = "es-ES",
				localizedNameList = "Casa");
		}

		task ("View page name is translated when change locale to es-ES") {
			Navigator.openSiteURL(siteName = "Test Site Name");

			Page.viewTranslationsPG(
				defaultLanguageLocalizationMessage = "Display the page in English (United States).",
				pageName = "Casa",
				preferredLanguageLocalizationMessage = "Establecer español (España) como su idioma preferido.",
				sitePageURL = "${portalURL}/es/web/test-site-name/home",
				translationLanguageLocalizationMessage = "This page is displayed in Spanish (Spain).");
		}

		task ("Locale the page to English") {
			Navigator.openSiteURL(siteName = "Test Site Name");

			Page.localizedPG(pageLocale = "Display the page in English (United States)");
		}

		task ("Add child widget page") {
			JSONLayout.addPublicLayout(
				groupName = "Test Site Name",
				layoutName = "Blog Page",
				parentLayoutName = "Home");
		}

		task ("Add a translation of child page to es-ES") {
			PagesAdmin.openPagesAdmin(siteURLKey = "test-site-name");

			PagesAdmin.gotoPageEllipsisMenuItem(
				menuItem = "Configure",
				pageName = "Blog Page",
				parentPagesHierarchy = "Home");

			PagesAdmin.addLocalizedName(
				localeList = "es-ES",
				localizedNameList = "Blog Página");
		}

		task ("View child page name is transled when changing site language to es-ES") {
			Navigator.openSiteURL(siteName = "Test Site Name");

			Page.viewTranslationsPG(
				childPageName = "Blog Página",
				defaultLanguageLocalizationMessage = "Display the page in English (United States).",
				preferredLanguageLocalizationMessage = "Establecer español (España) como su idioma preferido.",
				sitePageURL = "${portalURL}/es/web/test-site-name/blog-page",
				translationLanguageLocalizationMessage = "This page is displayed in Spanish (Spain).");
		}
	}

}