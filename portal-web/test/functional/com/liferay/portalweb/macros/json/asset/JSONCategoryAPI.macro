definition {

	@summary = "Directly calls the JSON WS API to add a category"
	macro _addCategory {
		Variables.assertDefined(parameterList = "${groupId},${parentCategoryId},${titleMap},${descriptionMap},${vocabularyId},${serviceContext}");

		var portalURL = JSONCompany.getPortalURL();

		var curl = '''
			${portalURL}/api/jsonws/assetcategory/add-category \
			-u test@liferay.com:test \
				-d groupId=${groupId} \
				-d parentCategoryId=${parentCategoryId} \
				-d titleMap=${titleMap} \
				-d descriptionMap=${descriptionMap} \
				-d vocabularyId=${vocabularyId} \
				-d categoryProperties= \
				-d serviceContext=${serviceContext}
		''';
		var categoryId = JSONCurlUtil.post("${curl}", "$.categoryId");

		return "${categoryId}";
	}

	@summary = "Directly calls the JSON WS API to add a vocabulary"
	macro _addVocabulary {
		Variables.assertDefined(parameterList = "${groupId},${titleMap},${descriptionMap},${serviceContext},${visibilityType}");

		var portalURL = JSONCompany.getPortalURL();

		var curl = '''
			${portalURL}/api/jsonws/assetvocabulary/add-vocabulary \
				-u test@liferay.com:test \
				-d externalReferenceCode = \
				-d groupId=${groupId} \
				-d title= \
				-d titleMap=${titleMap} \
				-d descriptionMap=${descriptionMap} \
				-d Settings= \
				-d visibilityType=${visibilityType} \
				-d serviceContext=${serviceContext}
		''';

		com.liferay.poshi.runner.util.JSONCurlUtil.post("${curl}");
	}

	@summary = "Directly calls the JSON WS API to delete category"
	macro _deleteCategory {
		Variables.assertDefined(parameterList = "${categoryId}");

		var portalURL = JSONCompany.getPortalURL();

		var curl = '''
			${portalURL}/api/jsonws/assetcategory/delete-category \
				-u test@liferay.com:test \
				-d categoryId=${categoryId}
		''';

		com.liferay.poshi.runner.util.JSONCurlUtil.post("${curl}");
	}

	@summary = "Directly calls the JSON WS API to delete vocabulary"
	macro _deleteVocabulary {
		Variables.assertDefined(parameterList = "${vocabularyId}");

		var portalURL = JSONCompany.getPortalURL();

		var curl = '''
			${portalURL}/api/jsonws/assetvocabulary/delete-vocabulary \
				-u test@liferay.com:test \
				-d vocabularyId=${vocabularyId}
		''';

		com.liferay.poshi.runner.util.JSONCurlUtil.post("${curl}");
	}

	@summary = "Directly calls the JSON WS API to get categoryId"
	macro _getCategoryIdByName {
		Variables.assertDefined(parameterList = "${groupId},${categoryName},${vocabularyId}");

		var portalURL = JSONCompany.getPortalURL();

		var curl = '''
			${portalURL}/api/jsonws/assetcategory/get-vocabulary-categories \
				-u test@liferay.com:test \
				-d groupId=${groupId} \
				-d <CURL_DATA[name=${categoryName}]CURL_DATA> \
				-d vocabularyId=${vocabularyId} \
				-d <CURL_DATA[start=-1]CURL_DATA> \
				-d <CURL_DATA[end=-1]CURL_DATA> \
				-d -orderByComparator=
		''';
		var categoryId = JSONCurlUtil.post("${curl}", "$.[?(@['titleCurrentValue'] == '${categoryName}')]['categoryId']");

		if ("${categoryId}" == "") {
			fail("FAIL. Cannot find category.");
		}

		return "${categoryId}";
	}

	macro _getVocabularyCategoriesIds {
		Variables.assertDefined(parameterList = "${vocabularyId}");

		var portalURL = JSONCompany.getPortalURL();

		var curl = '''
            ${portalURL}/api/jsonws/assetcategory/get-vocabulary-categories \
                -u test@liferay.com:test \
                -d vocabularyId=${vocabularyId} \
                -d "start=-1" \
                -d "end=-1" \
                -d -orderByComparator=
        ''';

		echo("# Get Categories IDs");

		echo("curl: ${curl}");

		var categoriesId = JSONCurlUtil.post("${curl}", "$..['categoryId']");

		echo("categoriesId: ${categoriesId}");

		return "${categoriesId}";
	}

	@summary = "Get the categoryId from any categoryby using groupId and vocabularyName"
	macro _getVocabularyIdByName {
		Variables.assertDefined(parameterList = "${groupId},${vocabularyName}");

		var portalURL = JSONCompany.getPortalURL();

		var curl = '''
			${portalURL}/api/jsonws/assetvocabulary/get-group-vocabularies \
				-u test@liferay.com:test \
				-d groupId=${groupId}
		''';
		var vocabularyId = JSONCurlUtil.post("${curl}", "$.[?(@['titleCurrentValue'] == '${vocabularyName}')]['vocabularyId']");

		if ("${vocabularyId}" == "") {
			fail("FAIL. Cannot find vocabulary.");
		}

		return "${vocabularyId}";
	}

	@summary = "Directly calls the JSON WS API to update a category"
	macro _updateCategory {
		Variables.assertDefined(parameterList = "${categoryId},${parentCategoryId},${titleMap},${descriptionMap},${vocabularyId}");

		var portalURL = JSONCompany.getPortalURL();

		var curl = '''
			${portalURL}/api/jsonws/assetcategory/update-category \
				-u test@liferay.com:test \
				-d categoryId=${categoryId} \
				-d parentCategoryId=${parentCategoryId} \
				-d titleMap=${titleMap} \
				-d descriptionMap=${descriptionMap} \
				-d vocabularyId=${vocabularyId} \
				-d categoryProperties=
		''';

		com.liferay.poshi.runner.util.JSONCurlUtil.post("${curl}");
	}

}