@component-name = "portal-wcm"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.component.names = "Personalization and Customization";
	property testray.main.component.name = "Site Pages Administration";

	setUp {
		task ("Set up instance and sign in") {
			TestCase.setUpPortalInstance();

			User.firstLoginPG();
		}

		task ("Add a site") {
			JSONGroup.addGroup(groupName = "Test Site Name");
		}

		task ("Add a widget page") {
			JSONLayout.addPublicLayout(
				groupName = "Test Site Name",
				layoutName = "Test Page Name");
		}
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			User.logoutAndLoginPG(userLoginEmailAddress = "test@liferay.com");

			Role.tearDownCP();

			JSONGroup.deleteGroupByName(groupName = "Test Site Name");

			JSONUser.tearDownNonAdminUsers();
		}
	}

	@description = "The user with site member permissions can add and remove a portlet from customizable view."
	@priority = "4"
	test AddAndRemoveWidgetAsSiteMember {
		property custom.properties = "jsonws.web.service.paths.excludes=";

		task ("Enable customizable to page then select the first column") {
			PagesAdmin.openPagesAdmin(siteURLKey = "test-site-name");

			PageCustomizations.enableSectionCustomizableCP(
				columnId = "1",
				pageName = "Test Page Name");
		}

		task ("View Page without my customizations") {
			Navigator.gotoSitePage(
				pageName = "Test Page Name",
				siteName = "Test Site Name");

			PageCustomizations.configureCustomization(menuItem = "View Page without my customizations");
		}

		task ("Add an Asset Publisher and Language Selector widget to page") {
			for (var widgetName : list "Asset Publisher,Language Selector") {
				JSONLayout.addWidgetToPublicLayout(
					groupName = "Test Site Name",
					layoutName = "Test Page Name",
					widgetName = "${widgetName}");
			}
		}

		task ("Add a new user") {
			JSONUser.addUser(
				userEmailAddress = "userea@liferay.com",
				userFirstName = "userfn",
				userLastName = "userln",
				userScreenName = "usersn");

			JSONUser.setFirstPassword(
				agreeToTermsAndAnswerReminderQuery = "true",
				requireReset = "false",
				userEmailAddress = "userea@liferay.com");
		}

		task ("Assign the user to site") {
			JSONUser.addUserToSite(
				groupName = "Test Site Name",
				userEmailAddress = "userea@liferay.com");
		}

		task ("Sign in as new user") {
			User.logoutAndLoginPG(userLoginEmailAddress = "userea@liferay.com");
		}

		task ("Add a Web Content Display widget to page") {
			Navigator.gotoSitePage(
				pageName = "Test Page Name",
				siteName = "Test Site Name");

			Portlet.addPG(portletName = "Web Content Display");
		}

		task ("View portlets are shown") {
			for (var portletName : list "Web Content Display,Asset Publisher,Language Selector") {
				Portlet.viewSpecificPG(portletName = "${portletName}");
			}
		}

		task ("Delete Language Selector widget") {
			Portlet.deletePG(portletName = "Language Selector");
		}

		task ("View Language Selector widget is not shown") {
			Portlet.viewNoSpecificPG(portletName = "Language Selector");
		}
	}

	@description = "A new user can add a widget portlet with customize permission."
	@priority = "4"
	test AddWidgetAsUserWithCustomizePermission {
		property custom.properties = "jsonws.web.service.paths.excludes=";

		task ("Enable customizable to page then select the first column") {
			PagesAdmin.openPagesAdmin(siteURLKey = "test-site-name");

			PageCustomizations.enableSectionCustomizableCP(
				columnId = "1",
				pageName = "Test Page Name");
		}

		task ("Add a new user") {
			JSONUser.addUser(
				userEmailAddress = "userea@liferay.com",
				userFirstName = "userfn",
				userLastName = "userln",
				userScreenName = "usersn");

			JSONUser.setFirstPassword(
				agreeToTermsAndAnswerReminderQuery = "true",
				requireReset = "false",
				userEmailAddress = "userea@liferay.com");
		}

		task ("Add a new regular role") {
			JSONRole.addRegularRole(roleTitle = "Regular Role Name");
		}

		task ("Define the permissions of new role") {
			Permissions.definePermissionViaJSONAPI(
				resourceAction = "CUSTOMIZE",
				resourceName = "com.liferay.portal.kernel.model.Layout",
				roleTitle = "Regular Role Name");
		}

		task ("Assign the regular role to new user") {
			JSONRole.assignRoleToUser(
				roleTitle = "Regular Role Name",
				userEmailAddress = "userea@liferay.com");
		}

		task ("Sign in as new user") {
			User.logoutAndLoginPG(userLoginEmailAddress = "userea@liferay.com");
		}

		task ("Add a Navigation Menu widget to page") {
			JSONLayout.addWidgetToPublicLayout(
				groupName = "Test Site Name",
				layoutName = "Test Page Name",
				widgetName = "Menu Display Widget");
		}

		task ("View Navigation Menu widget is shown") {
			Navigator.gotoSitePage(
				pageName = "Test Page Name",
				siteName = "Test Site Name");

			Portlet.viewSpecificPG(portletName = "Menu Display Widget");
		}
	}

	@description = "The configurations applied to a customizable page should not affect all users."
	@priority = "4"
	test PortletConfigurationChangesWithinCustomizableColumnShouldNotAffectAllUsers {
		property custom.properties = "jsonws.web.service.paths.excludes=";

		task ("Enable customizable to page then select the first column") {
			PagesAdmin.openPagesAdmin(siteURLKey = "test-site-name");

			PageCustomizations.enableSectionCustomizableCP(
				columnId = "1",
				pageName = "Test Page Name");
		}

		task ("Add a Wiki widget to page") {
			JSONLayout.addWidgetToPublicLayout(
				groupName = "Test Site Name",
				layoutName = "Test Page Name",
				widgetName = "Wiki");
		}

		task ("View customize is enabled to page") {
			Navigator.gotoSitePage(
				pageName = "Test Page Name",
				siteName = "Test Site Name");

			PageCustomizations.viewCustomizablePagePG();
		}

		task ("View Page without my customizations") {
			PageCustomizations.configureCustomization(menuItem = "View Page without my customizations");

			Portlet.viewSpecificPG(portletName = "Wiki");
		}

		task ("Add a new user") {
			JSONUser.addUser(
				userEmailAddress = "userea@liferay.com",
				userFirstName = "userfn",
				userLastName = "userln",
				userScreenName = "usersn");

			JSONUser.setFirstPassword(
				agreeToTermsAndAnswerReminderQuery = "true",
				requireReset = "false",
				userEmailAddress = "userea@liferay.com");
		}

		task ("Assign the user to site") {
			JSONUser.addUserToSite(
				groupName = "Test Site Name",
				userEmailAddress = "userea@liferay.com");
		}

		task ("Sign in as new user") {
			User.logoutAndLoginPG(userLoginEmailAddress = "userea@liferay.com");
		}

		task ("Enable allow users to add wiki to any website") {
			Navigator.gotoSitePage(
				pageName = "Test Page Name",
				siteName = "Test Site Name");

			Portlet.configureSharingPG(
				checkAnyWebSiteToggle = "true",
				portletName = "Wiki");
		}

		task ("Sign in as Test Test") {
			User.logoutAndLoginPG(userLoginEmailAddress = "test@liferay.com");
		}

		task ("View allow user to add wiki to any website is disabled to Test Test") {
			Navigator.gotoSitePage(
				pageName = "Test Page Name",
				siteName = "Test Site Name");

			Portlet.viewConfigureSharingPG(
				checkedAnyWebSiteToggle = "false",
				portletName = "Wiki");
		}
	}

	@description = "User with site member and customize permission will only see portlets in customizable view."
	@priority = "4"
	test SiteMemberWithCustomizablePagePermissionsWillOnlySeeWidgetsInCustomizableView {
		property custom.properties = "jsonws.web.service.paths.excludes=";
		property test.name.skip.portal.instance = "PageCustomizations#SiteMemberWithCustomizablePagePermissionsWillOnlySeeWidgetsInCustomizableView";

		task ("Clean up permissions in server administration") {
			ApplicationsMenu.gotoPortlet(
				category = "System",
				panel = "Control Panel",
				portlet = "Server Administration");

			ServerAdministration.executeServerResourcesActions(actionsDescription = "Clean up permissions");
		}

		task ("Enable customizable to page then select the first column") {
			PagesAdmin.openPagesAdmin(siteURLKey = "test-site-name");

			PageCustomizations.enableSectionCustomizableCP(
				columnId = "1",
				pageName = "Test Page Name");
		}

		task ("View Page without my customizations") {
			Navigator.gotoSitePage(
				pageName = "Test Page Name",
				siteName = "Test Site Name");

			PageCustomizations.configureCustomization(menuItem = "View Page without my customizations");
		}

		task ("Add a Message Boards and Language Selector widgets to page") {
			for (var widgetName : list "Message Boards,Language Selector") {
				JSONLayout.addWidgetToPublicLayout(
					groupName = "Test Site Name",
					layoutName = "Test Page Name",
					widgetName = "${widgetName}");
			}
		}

		task ("Add a new user") {
			JSONUser.addUser(
				userEmailAddress = "userea@liferay.com",
				userFirstName = "userfn",
				userLastName = "userln",
				userScreenName = "usersn");

			JSONUser.setFirstPassword(
				agreeToTermsAndAnswerReminderQuery = "true",
				requireReset = "false",
				userEmailAddress = "userea@liferay.com");
		}

		task ("Add a new regular role") {
			JSONRole.addRegularRole(roleTitle = "Regular Role Name");
		}

		task ("Define the permissions of regular role") {
			Permissions.definePermissionViaJSONAPI(
				resourceAction = "CUSTOMIZE",
				resourceName = "com.liferay.portal.kernel.model.Layout",
				roleTitle = "Regular Role Name");
		}

		task ("Assign the regular role to new user") {
			JSONRole.assignRoleToUser(
				roleTitle = "Regular Role Name",
				userEmailAddress = "userea@liferay.com");
		}

		task ("Assign the user to site") {
			JSONUser.addUserToSite(
				groupName = "Test Site Name",
				userEmailAddress = "userea@liferay.com");
		}

		task ("Sign in as new user") {
			User.logoutAndLoginPG(userLoginEmailAddress = "userea@liferay.com");
		}

		task ("View portlets are shown in page without customization") {
			Navigator.gotoSitePage(
				pageName = "Test Page Name",
				siteName = "Test Site Name");

			PageCustomizations.configureCustomization(menuItem = "View Page without my customizations");

			for (var portletName : list "Message Boards,Language Selector") {
				Portlet.viewSpecificPG(portletName = "${portletName}");
			}
		}

		task ("Reset customization then view portlets are not shown") {
			PageCustomizations.configureCustomization(menuItem = "View My Customized Page");

			PageCustomizations.configureCustomization(menuItem = "Reset My Customizations");

			for (var portletName : list "Message Boards,Language Selector") {
				Portlet.viewNoSpecificPG(portletName = "${portletName}");
			}
		}

		task ("Sign in as Test Test") {
			User.logoutAndLoginPG(userLoginEmailAddress = "test@liferay.com");
		}

		task ("Define new permissions of regular role") {
			Permissions.definePermissionViaJSONAPI(
				resourceAction = "ADD_TO_PAGE",
				resourceName = "com_liferay_message_boards_web_portlet_MBPortlet",
				roleTitle = "Regular Role Name");

			Permissions.definePermissionViaJSONAPI(
				resourceAction = "ADD_TO_PAGE",
				resourceName = "com_liferay_site_navigation_language_web_portlet_SiteNavigationLanguagePortlet",
				roleTitle = "Regular Role Name");
		}

		task ("Sign in as new user") {
			User.logoutAndLoginPG(userLoginEmailAddress = "userea@liferay.com");
		}

		task ("View portlets are shown in page without my customizations") {
			Navigator.gotoSitePage(
				pageName = "Test Page Name",
				siteName = "Test Site Name");

			PageCustomizations.configureCustomization(menuItem = "View Page without my customizations");

			for (var portletName : list "Message Boards,Language Selector") {
				Portlet.viewSpecificPG(portletName = "${portletName}");
			}
		}
	}

}