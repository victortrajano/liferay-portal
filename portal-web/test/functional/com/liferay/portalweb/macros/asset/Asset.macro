definition {

	macro changePermission {
		SelectFrame(locator1 = "IFrame#MODAL_IFRAME");

		var key_userName = "${userName}";

		AssertTextEquals(
			locator1 = "Sharing#MANAGE_COLLABORATORS_NAME",
			value1 = "${userName}");

		Select(
			locator1 = "Sharing#MANAGE_COLLABORATORS_ACCESS_DROPDOWN",
			value1 = "${selectPermission}");

		AssertSelectedLabel(
			locator1 = "Sharing#MANAGE_COLLABORATORS_ACCESS_DROPDOWN",
			value1 = "${selectPermission}");

		Button.clickSave();

		SelectFrame(value1 = "relative=top");

		Alert.viewSuccessMessageText(successMessage = "Permissions Changed");
	}

	macro gotoManageCollaboratorsViaSharedByMe {
		var key_contentTitle = "${contentTitle}";

		Click(
			locator1 = "Sharing#SHARED_CONTENT_ELLIPSIS",
			value1 = "${contentTitle}");

		MenuItem.click(menuItem = "Manage Collaborators");
	}

	macro openTosharedContent {

		// You can use this navigation when you need to access the shared content quickly without checking the UI.

		if ("${navTab}" == "Shared with Me") {
			var incomingValue = "true";
		}
		else if ("${navTab}" == "Shared by Me") {
			var incomingValue = "false";
		}

		Navigator.openWithAppendToBaseURL(urlAppend = "group/${siteURLKey}/~/control_panel/manage?p_p_id=com_liferay_sharing_web_portlet_SharedAssetsPortlet&p_p_state=maximized&p_p_mode=view&_com_liferay_sharing_web_portlet_SharedAssetsPortlet_incoming=${incomingValue}");
	}

	macro setFilterAndOrder {
		if (isSet(restoreFilter)) {
			ManagementBar.setFilterAndOrder(filterBy = "All");
		}

		ManagementBar.setFilterAndOrder(
			filterBy = "${filterBy}",
			orderBy = "${orderBy}");

		if ("${filterBy}" == "Asset Types") {
			AssertTextEquals(
				locator1 = "Portlet#MODAL_TITLE",
				value1 = "Select Asset Type");

			SelectFrame(locator1 = "IFrame#MODAL_IFRAME");

			if ("${assetType}" == "Document") {
				var assetType = "${assetType}";
			}

			if ("${assetType}" == "Blogs Entry") {
				var assetType = "${assetType}";
			}

			Click(
				key_assetType = "${assetType}",
				locator1 = "Sharing#SHARED_CONTENT_ASSET_TYPE");

			SelectFrame(value1 = "relative=top");
		}
	}

	macro share {
		IFrame.selectModalFrame();

		if (isSet(noAutocomplete)) {
			Type.sendKeys(
				locator1 = "Sharing#MODAL_INVITE_TO_COLLABORATE_INPUT",
				value1 = "${userEmailAddress},");
		}
		else {
			Type.sendKeys(
				locator1 = "Sharing#MODAL_INVITE_TO_COLLABORATE_INPUT",
				value1 = "${userEmailAddress}");

			Click(locator1 = "Sharing#MODAL_INVITE_TO_COLLABORATE_DROPDOWN");

			var key_userName = "${userName}";

			AssertElementPresent(locator1 = "Sharing#MODAL_INVITE_TO_COLLABORATE_TAG");
		}

		if (isSet(shareable)) {
			FormFields.enableCheckbox(fieldName = "shareable");
		}
		else {
			FormFields.disableCheckbox(fieldName = "shareable");
		}

		if ("${sharingPermissions}" == "Update") {
			FormFields.selectRadioOption(
				fieldName = "sharingEntryPermission",
				radioOption = "Update");
		}
		else if ("${sharingPermissions}" == "Comment") {
			FormFields.selectRadioOption(
				fieldName = "sharingEntryPermission",
				radioOption = "Comment");
		}
		else {
			FormFields.viewRadioValue(
				fieldName = "sharingEntryPermission",
				fieldValue = "View");
		}

		Button.clickShare();

		SelectFrame(value1 = "relative=top");

		Alert.viewSuccessMessageText(successMessage = "The item was shared successfully.");
	}

}