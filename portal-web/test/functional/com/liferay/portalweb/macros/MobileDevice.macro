definition {

	macro addClassificationRuleCP {

		// Workaround for LPS-92682

		Click(locator1 = "Button#PLUS");

		AssertTextEquals.assertPartialText(
			locator1 = "Portlet#HEADER",
			value1 = "New Classification Rule");

		PortletEntry.inputName(name = "${classificationName}");

		if (isSet(classificationDescription)) {
			PortletEntry.inputDescription(description = "${classificationDescription}");
		}

		Panel.expandPanel(panel = "Operating System and Type");

		AddSelection(
			locator1 = "MobileDevice#ADD_CLASSIFICATION_RULE_OPERATING_SYSTEM",
			value1 = "${ruleOsScope}");

		Select(
			locator1 = "MobileDevice#ADD_CLASSIFICATION_RULE_DEVICE_TYPE",
			value1 = "${deviceType}");

		Click(locator1 = "Button#SAVE");

		Alert.viewSuccessMessage();
	}

	macro addDeviceFamilyCP {
		Click(locator1 = "Button#PLUS");

		AssertTextEquals.assertPartialText(
			locator1 = "Portlet#HEADER",
			value1 = "New Device Family");

		PortletEntry.inputName(name = "${deviceFamilyName}");

		if (isSet(deviceDescription)) {
			PortletEntry.inputDescription(description = "${deviceDescription}");
		}

		Click(locator1 = "Button#SAVE");

		Alert.viewSuccessMessage();
	}

	macro assertRedirectActionPG {
		var key_pageName = "${pageName}";

		AssertClick(
			locator1 = "Home#PAGE",
			value1 = "${pageName}");

		var key_pageName = "${redirectPageName}";

		AssertVisible(locator1 = "Home#PAGE_ACTIVE");
	}

	macro gotoMobileDeviceFamily {
		var key_deviceFamilyName = "${mobileDeviceFamilyName}";

		AssertClick(
			locator1 = "MobileDevice#DEVICE_FAMILY_TABLE_NAME_LINK",
			value1 = "${mobileDeviceFamilyName}");
	}

	macro openMobileDeviceAdmin {
		Navigator.openWithAppendToBaseURL(urlAppend = "group/${siteURLKey}/~/control_panel/manage?p_p_id=com_liferay_mobile_device_rules_web_portlet_MDRPortlet");
	}

	macro previewPG {
		Click(locator1 = "ControlMenu#SIMULATION");

		Pause(locator1 = "2000");

		AssertElementNotPresent(locator1 = "IFrame#LOADING_MESSAGE");

		var key_deviceType = "${deviceType}";

		if ("${deviceType}" == "smartphone") {
			takeScreenshot();

			Click(locator1 = "ControlMenuPreviewPanel#PREVIEW_DATA_DEVICE_BUTTON");

			AssertElementPresent(locator1 = "ControlMenuPreviewPanel#PREVIEW_DEVICE_IFRAME");

			takeScreenshot();
		}
		else if ("${deviceType}" == "tablet") {
			Click(locator1 = "ControlMenuPreviewPanel#PREVIEW_DATA_DEVICE_BUTTON");

			takeScreenshot();

			Click(locator1 = "ControlMenuPreviewPanel#PREVIEW_DATA_DEVICE_BUTTON");

			AssertElementPresent(locator1 = "ControlMenuPreviewPanel#PREVIEW_DEVICE_IFRAME");

			takeScreenshot();
		}
		else if ("${deviceType}" == "desktop") {
			Click(locator1 = "ControlMenuPreviewPanel#PREVIEW_DATA_DEVICE_BUTTON");

			AssertElementPresent(locator1 = "ControlMenuPreviewPanel#PREVIEW_DEVICE_GENERAL_IFRAME");

			takeScreenshot();
		}
		else if ("${deviceType}" == "custom") {
			Click(locator1 = "ControlMenuPreviewPanel#PREVIEW_DATA_DEVICE_BUTTON");

			AssertElementPresent(locator1 = "ControlMenuPreviewPanel#PREVIEW_DEVICE_GENERAL_IFRAME");

			takeScreenshot();

			if (isSet(height)) {
				Type(
					locator1 = "ControlMenuPreviewPanel#PREVIEW_CUSTOM_HEIGHT_FIELD",
					value1 = "${height}");
			}
			else {
				var height = "400";
			}

			if (isSet(width)) {
				Type(
					locator1 = "ControlMenuPreviewPanel#PREVIEW_CUSTOM_WIDTH_FIELD",
					value1 = "${width}");
			}
			else {
				var width = "400";
			}

			takeScreenshot();
		}

		SelectFrame.selectFrameNoLoading(locator1 = "ControlMenuPreviewPanel#PREVIEW_DEVICE_GENERAL_IFRAME");
	}

}