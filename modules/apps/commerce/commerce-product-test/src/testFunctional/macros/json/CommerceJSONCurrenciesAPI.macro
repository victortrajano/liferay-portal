definition {

	macro _deleteCommerceCurrencies {
		var baseURL = "${baseURL}";

		if (!(isSet(baseURL))) {
			var baseURL = PropsUtil.get("portal.url");
		}

		var commerceCurrenciesIds = CommerceJSONCurrenciesAPI._getCommerceCurrencies();

		if ("${commerceCurrenciesIds}" != "") {
			for (var commerceCurrenciesId : list "${commerceCurrenciesIds}") {
				echo("Deleting Commerce Currency with id: ${commerceCurrenciesId}");

				var curl = '''
					${baseURL}/api/jsonws/commerce.commercecurrency/delete-commerce-currency \
						-u test@liferay.com:test \
						-d commerceCurrencyId=${commerceCurrenciesId}
				''';

				JSONCurlUtil.post("${curl}");
			}
		}
		else {
			echo("No Commerce Currency to be deleted");
		}
	}

	macro _getCommerceCurrencies {
		var baseURL = "${baseURL}";

		if (!(isSet(baseURL))) {
			var baseURL = PropsUtil.get("portal.url");
		}

		var companyId = JSONCompany.getCompanyId();

		var curl = '''
			  ${baseURL}/api/jsonws/commerce.commercecurrency/get-commerce-currencies \
				  -u test@liferay.com:test \
				  -d companyId=${companyId} \
				  -d "start=-1" \
				  -d "end=-1" \
				  -d -orderByComparator=''
		''';

		echo("curl: ${curl}");

		var commerceCurrencies = JSONCurlUtil.post("${curl}", "$..['commerceCurrencyId']");

		echo("commerceCurrencies: ${commerceCurrencies}");

		return "${commerceCurrencies}";
	}

}