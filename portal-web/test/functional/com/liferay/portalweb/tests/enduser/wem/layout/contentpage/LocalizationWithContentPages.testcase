@component-name = "portal-wcm"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Content Pages";

	setUp {
		task ("Set up instance and sign in") {
			TestCase.setUpPortalInstance();

			User.firstLoginPG();
		}

		task ("Add a site") {
			JSONGroup.addGroup(groupName = "Test Site Name");
		}

		task ("Add a content page") {
			JSONLayout.addPublicLayout(
				groupName = "Test Site Name",
				layoutName = "Content Page Name",
				type = "content");
		}
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			JSONGroup.deleteGroupByName(groupName = "Test Site Name");
		}
	}

	@description = "This is a use case for LPS-106776. Add a translation to inline content."
	@priority = "5"
	test AddTranslationToInlineContent {
		property portal.acceptance = "true";

		task ("Add a Paragraph fragment to the content page") {
			ContentPagesNavigator.openEditContentPage(
				pageName = "Content Page Name",
				siteName = "Test Site Name");

			PageEditor.addFragment(
				collectionName = "Basic Components",
				fragmentName = "Paragraph");
		}

		task ("Edit the inline content of Paragraph") {
			PageEditor.editFragmentText(
				fragmentName = "Paragraph",
				id = "element-text",
				text = "New editable fragment en-US text");
		}

		task ("Add a translation to inline content") {
			PageEditor.changeLocale(
				currentLocale = "en-US",
				locale = "ca-ES");

			PageEditor.editFragmentText(
				fragmentName = "Paragraph",
				id = "element-text",
				text = "New editable fragment ca-ES text");

			PageEditor.clickPublish();
		}

		task ("Navigate to the content page in Edit mode") {
			ContentPagesNavigator.openEditContentPage(
				pageName = "Content Page Name",
				siteName = "Test Site Name");
		}

		task ("View inline content in en-US") {
			PageEditor.viewFragmentEditableText(
				editableText = "New editable fragment en-US text",
				fragmentName = "Paragraph",
				id = "element-text");
		}

		task ("View localized inline content") {
			PageEditor.changeLocale(
				currentLocale = "en-US",
				locale = "ca-ES");

			PageEditor.viewFragmentEditableText(
				editableText = "New editable fragment ca-ES text",
				fragmentName = "Paragraph",
				id = "element-text");
		}
	}

	@description = "This is a test for LPS-140483. The site admin could create page template from content page with translation."
	@priority = "4"
	test CreatePageTemplateFromContentPageWithTranslation {
		task ("Add a page template collection") {
			JSONLayoutpagetemplate.addLayoutPageTemplateCollection(
				groupName = "Test Site Name",
				layoutPageTemplateCollectionName = "Page Template Collection");
		}

		task ("Add a Heading fragment to content page") {
			ContentPagesNavigator.openEditContentPage(
				pageName = "Content Page Name",
				siteName = "Test Site Name");

			PageEditor.addFragment(
				collectionName = "Basic Components",
				fragmentName = "Heading");
		}

		task ("Add translation to inline text of Heading in es-ES") {
			PageEditor.changeLocale(
				currentLocale = "en-US",
				locale = "es-ES");

			PageEditor.editFragmentText(
				fragmentName = "Heading",
				id = "element-text",
				text = "Ejemplo de encabezado");
		}

		task ("Create a content page template from the content page under page template collection") {
			PageEditor.createPageTemplate(
				collectionName = "Page Template Collection",
				pageTemplateName = "Content Page Template Name");
		}

		task ("View the content page template shown in Page Templates admin") {
			PageTemplatesAdmin.openPageTemplatesAdmin(siteURLKey = "test-site-name");

			Navigator.gotoNavNested(navNested = "Page Template Collection");

			PageTemplatesAdmin.viewPageTemplatePresent(pageTemplateName = "Content Page Template Name");

			PageTemplatesAdmin.viewEntryWorkflowStatus(
				entry = "Content Page Template Name",
				workflowStatus = "Draft");
		}

		task ("View the default inline text shown in content page template in en-US") {
			PageTemplatesAdmin.gotoPageTemplate(pageTemplateName = "Content Page Template Name");

			PageEditor.viewFragmentEditableText(
				editableText = "Heading Example",
				fragmentName = "Heading",
				id = "element-text");
		}

		task ("View the translated inline text shown in content page template in es-US") {
			PageEditor.changeLocale(
				currentLocale = "en-US",
				locale = "es-ES");

			PageEditor.viewFragmentEditableText(
				editableText = "Ejemplo de encabezado",
				fragmentName = "Heading",
				id = "element-text");
		}
	}

	@description = "This is a test for LPS-139064. Users can preview localized draft for a content page."
	@priority = "3"
	test PreviewLocalizedDraftContentPage {
		task ("Add a Button fragment to page") {
			ContentPagesNavigator.openEditContentPage(
				pageName = "Content Page Name",
				siteName = "Test Site Name");

			PageEditor.addFragment(
				collectionName = "Basic Components",
				fragmentName = "Button");
		}

		task ("Change the locale to ca-ES") {
			PageEditor.changeLocale(
				currentLocale = "en-US",
				locale = "ca-ES");
		}

		task ("Add the localized inline text") {
			PageEditor.editFragmentText(
				fragmentName = "Button",
				id = "link",
				text = "Aneu a algun lloc");
		}

		task ("Navigate to the Pages admin") {
			PagesAdmin.openPagesAdmin(siteURLKey = "test-site-name");
		}

		task ("Change the user language to ca-ES") {
			Navigator.ensureLocaleURL(locale = "ca");
		}

		task ("Preview the localized draft content page in a new tab") {
			PagesAdmin.gotoPageEllipsisMenuItem(
				menuItem = "Previsualitza l'esborrany",
				pageName = "Content Page Name");

			SelectWindow.selectPopUp(locator1 = "null");

			ControlMenu.viewHeaderTitle(headerTitle = "Content Page Name *");

			ContentPages.viewFragmentText(
				fragmentName = "button",
				id = "link",
				text = "Aneu a algun lloc");
		}
	}

	@description = "This is a test for LPS-122148. Search localized inline text items in Page Content panel."
	@priority = "4"
	test SearchLocalizedInlineTextItemsInPageContentPanel {
		task ("Add a Heading fragment to page") {
			ContentPagesNavigator.openEditContentPage(
				pageName = "Content Page Name",
				siteName = "Test Site Name");

			PageEditor.addFragment(
				collectionName = "Basic Components",
				fragmentName = "Heading");
		}

		task ("Add a Banner Center below the Heading") {
			PageEditor.addFragment(
				collectionName = "Featured Content",
				composition = "true",
				fragmentName = "Banner Center",
				targetFragmentName = "Heading");
		}

		task ("Edit the inline text of Heading") {
			PageEditor.editFragmentText(
				fragmentName = "Heading",
				id = "element-text",
				text = "Edited Text");
		}

		task ("Edit the inline text of title field in Banner Center") {
			PageEditor.editFragmentText(
				fragmentName = "Heading",
				id = "element-text",
				index = "2",
				text = "Edited Title");
		}

		task ("Edit the inline text of subtitle field in Banner Center") {
			PageEditor.editFragmentText(
				fragmentName = "Paragraph",
				id = "element-text",
				text = "Edited Subtitle");
		}

		task ("Change locale to es-ES") {
			PageEditor.changeLocale(
				currentLocale = "en-US",
				locale = "es-ES");
		}

		task ("Edit the localized inline text of Heading") {
			PageEditor.editFragmentText(
				fragmentName = "Heading",
				id = "element-text",
				text = "Texto editado");
		}

		task ("Edit the localized inline text of title field in Banner Center") {
			PageEditor.editFragmentText(
				fragmentName = "Heading",
				id = "element-text",
				index = "2",
				text = "Título editado");
		}

		task ("Change locale to en-US") {
			PageEditor.changeLocale(
				currentLocale = "es-ES",
				locale = "en-US");
		}

		task ("View the inline texts in Page Content tab") {
			PageEditor.gotoTab(tabName = "Browser");

			Navigator.gotoNavTab(navTab = "Page Content");

			for (var itemName : list "Edited Text,Edited Title,Edited Subtitle") {
				AssertVisible(
					key_itemName = "${itemName}",
					locator1 = "PageEditor#PAGE_CONTENTS_ITEM");
			}
		}

		task ("Change locale to es-ES") {
			PageEditor.changeLocale(
				currentLocale = "en-US",
				locale = "es-ES");
		}

		task ("View the inline texts in Page Content tab") {
			for (var itemName : list "Texto editado,Título editado,Edited Subtitle") {
				AssertVisible(
					key_itemName = "${itemName}",
					locator1 = "PageEditor#PAGE_CONTENTS_ITEM");
			}
		}

		task ("Search the localized inline text in Page Content panel") {
			PageEditor.searchInPageContentPanel(searchTerm = "Texto");

			AssertVisible(
				key_itemName = "Texto editado",
				locator1 = "PageEditor#PAGE_CONTENTS_ITEM");

			for (var itemName : list "Título editado,Edited Subtitle") {
				AssertElementNotPresent(
					key_itemName = "${itemName}",
					locator1 = "PageEditor#PAGE_CONTENTS_ITEM");
			}

			PageEditor.searchInPageContentPanel(searchTerm = "Título");

			AssertVisible(
				key_itemName = "Título editado",
				locator1 = "PageEditor#PAGE_CONTENTS_ITEM");

			for (var itemName : list "Texto editado,Edited Subtitle") {
				AssertElementNotPresent(
					key_itemName = "${itemName}",
					locator1 = "PageEditor#PAGE_CONTENTS_ITEM");
			}
		}
	}

}