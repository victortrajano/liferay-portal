definition {

	macro _addOrganization {
		Variables.assertDefined(parameterList = "${organizationName}");

		var organizationNameEncoded = URLUtil.encodeUTF8("${organizationName}");

		if (!(isSet(organizationSite))) {
			var organizationSite = "false";
		}

		if (isSet(parentOrganizationId)) {
			var parentOrganizationId = "${parentOrganizationId}";
		}
		else {
			var parentOrganizationId = "0";
		}

		if (isSet(organizationType)) {
			var organizationType = "${organizationType}";
		}
		else {
			var organizationType = "organization";
		}

		var portalURL = JSONCompany.getPortalURL();

		var curl = '''
			${portalURL}/api/jsonws/organization/add-organization \
				-u test@liferay.com:test \
				-d parentOrganizationId=${parentOrganizationId} \
				-d name=${organizationNameEncoded} \
				-d type=${organizationType} \
				-d regionId=0 \
				-d countryId=0 \
				-d statusId=12017 \
				-d comments= \
				-d site=${organizationSite}
		''';

		com.liferay.poshi.runner.util.JSONCurlUtil.post("${curl}");
	}

	macro _addOrganizationToSite {
		Variables.assertDefined(parameterList = "${groupId},${organizationId}");

		var portalURL = JSONCompany.getPortalURL();

		var curl = '''
			${portalURL}/api/jsonws/organization/add-group-organizations/group-id/${groupId}/organization-ids/${organizationId} \
				-u test@liferay.com:test
		''';

		com.liferay.poshi.runner.util.JSONCurlUtil.post("${curl}");
	}

	macro _assignPasswordPolicyToOrganization {
		Variables.assertDefined(parameterList = "${organizationId},${passwordPolicyId}");

		var portalURL = JSONCompany.getPortalURL();

		var curl = '''
			${portalURL}/api/jsonws/organization/add-password-policy-organizations \
				-u test@liferay.com:test \
				-d organizationIds=${organizationId} \
				-d passwordPolicyId=${passwordPolicyId}
		''';

		JSONCurlUtil.post("${curl}");
	}

	macro _assignUserToOrganization {
		Variables.assertDefined(parameterList = "${organizationId},${userId}");

		var portalURL = JSONCompany.getPortalURL();

		var curl = '''
			${portalURL}/api/jsonws/user/add-organization-users \
				-u test@liferay.com:test \
				-d organizationId=${organizationId} \
				-d userIds=${userId}
		''';

		com.liferay.poshi.runner.util.JSONCurlUtil.post("${curl}");
	}

	macro _deleteOrganization {
		Variables.assertDefined(parameterList = "${organizationId}");

		var portalURL = JSONCompany.getPortalURL();

		var curl = '''
			${portalURL}/api/jsonws/organization/delete-organization \
				-u test@liferay.com:test \
				-d organizationId=${organizationId}
		''';

		com.liferay.poshi.runner.util.JSONCurlUtil.post("${curl}");
	}

}