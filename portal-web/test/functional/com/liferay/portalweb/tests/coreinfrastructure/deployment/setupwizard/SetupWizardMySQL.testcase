@component-name = "portal-deployment-mysql"
definition {

	property database.types = "mysql";
	property portal.release = "true";
	property portal.upstream = "true";
	property setup.wizard.enabled = "true";
	property testray.main.component.name = "Setup Wizard";

	@priority = "4"
	test MySQLtoMySQLEditedAdministratorUser {
		property test.name.skip.portal.instance = "SetupWizardMySQL#MySQLtoMySQLEditAdministratorUser";

		SetupWizard.viewDefaultPortalNamePG();

		SetupWizard.configureAdministratorUserPG(
			administratorEmail = "testsetupwizard@liferay.com",
			administratorFirstName = "TestSetupWizardFN",
			administratorLastName = "TestSetupWizardLN");

		SetupWizard.configureDatabasePG(
			databaseName = "lportal",
			databasePassword = PropsUtil.get("database.mysql.password"),
			databaseType = "MySQL",
			databaseUserName = PropsUtil.get("database.mysql.username"),
			jdbcDriverClassName = PropsUtil.get("database.mysql.driver"),
			jdbcURL = PropsUtil.get("database.mysql.url"));

		SetupWizard.finishConfigurationPG();

		SetupWizard.viewConfigurationSavedPG();

		Portlet.shutdownServer();

		Portlet.startServer(
			base_branch = "${base_branch}",
			deleteLiferayHome = "false");

		SetupWizard.viewConfiguredDatabaseInConsole(databaseType = "${databaseType}");

		if (isSet(virtualHostsURL)) {
			Navigator.openSpecificURL(url = "${virtualHostsURL}");
		}
		else {
			Navigator.openURL();
		}

		User.acceptEndUserLicenseAgreement();

		User.resetPassword(
			resetPassword = "${resetPassword}",
			setupWizardEnabled = "${setupWizardEnabled}",
			syntaxCheckingEnabled = "${syntaxCheckingEnabled}");

		User.answerPasswordReminder();

		AssertElementPresent(locator1 = "UserBar#USER_AVATAR_IMAGE");

		User.viewLoggedInPG();

		Navigator.openURL();

		Navigator.gotoAccountSettings();

		AssertTextEquals(
			locator1 = "TextInput#EMAIL_ADDRESS",
			value1 = "testsetupwizard@liferay.com");

		AssertTextEquals(
			locator1 = "TextInput#FIRST_NAME",
			value1 = "TestSetupWizardFN");

		AssertTextEquals(
			locator1 = "TextInput#LAST_NAME",
			value1 = "TestSetupWizardLN");

		AssertTextEquals(
			locator1 = "TextInput#SCREEN_NAME",
			value1 = "testsetupwizard");
	}

}